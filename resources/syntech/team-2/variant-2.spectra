import "DwyerPatterns.spectra"

spec Variant2

define NumObstacles := 5;
define DX := 4;
define DY := 4;

type DimX = Int(0..DX);
type DimY = Int(0..DY);
type Dim = Int(0..4);
 
sys DimX robotX;
sys DimY robotY;


/*
 * random environment variables that are fixed throughout the run
 */
env Dim[2] cellA;
env Dim[2] cellB;
env Dim[2] cellC;
 
/*
 * we'll define 'home' as "the robot is at (0,0)
 * same for each target
 */
define
	home := robotX = 0 & robotY =  0;
	atCellA := (robotX = cellA[0]) & (robotY = cellA[1]);
	atCellB := (robotX = cellB[0]) & (robotY = cellB[1]);
	atCellC := (robotX = cellC[0]) & (robotY = cellC[1]);
	

// targets are not on obstacles
//Assume the targets are not at (4,4)
predicate targetNotOnObstacleOrHomeOrBootomRight(Dim cellX, Dim cellY):
	(!(cellX = 4 & cellY = 4)) & (!(cellX = 0 & cellY = 0)) & (!(cellX = 0 & cellY = 3)) & (!(cellX = 1 & cellY = 1)) & (!(cellX = 2 & cellY = 1)) & (!(cellX = 3 & cellY = 3)) & (!(cellX = 3 & cellY = 4));

asm alw targetNotOnObstacleOrHomeOrBootomRight(cellA[0], cellA[1]);
asm alw targetNotOnObstacleOrHomeOrBootomRight(cellB[0], cellB[1]);
asm alw targetNotOnObstacleOrHomeOrBootomRight(cellC[0], cellC[1]);


// the robot does not step on obstacles
predicate robotNotOnObstacle(DimX robotX, DimY robotY):
	(!(robotX = 0 & robotY = 3)) & (!(robotX = 1 & robotY = 1)) & (!(robotX = 2 & robotY = 1)) & (!(robotX = 3 & robotY = 3)) & (!(robotX = 3 & robotY = 4));

gar alw robotNotOnObstacle(robotX, robotY);


// targets are in different locations
predicate targetsAreDifferent(Dim cell1X, Dim cell1Y, Dim cell2X, Dim cell2Y):
	!((cell1X = cell2X) & (cell1Y = cell2Y));

asm alw targetsAreDifferent(cellA[0], cellA[1], cellB[0], cellB[1]);
asm alw targetsAreDifferent(cellA[0], cellA[1], cellC[0], cellC[1]);
asm alw targetsAreDifferent(cellB[0], cellB[1], cellC[0], cellC[1]);


// targets do not move
asm alw cellA[0] = next(cellA[0]) & cellA[1] = next(cellA[1]);
asm alw cellB[0] = next(cellB[0]) & cellB[1] = next(cellB[1]);
asm alw cellC[0] = next(cellC[0]) & cellC[1] = next(cellC[1]);


// visit each target and home fairly
gar alwEv atCellA;
gar alwEv atCellB;
gar alwEv atCellC;
//gar alwEv home;

// start at home
gar ini home;

//Assume the targets are at first row - row 0
predicate targetOnFirstRow(Dim cellX, Dim cellY):
	((cellX = 1 & cellY = 0)) | ((cellX = 2 & cellY = 0)) | ((cellX = 3 & cellY = 0)) | ((cellX = 4 & cellY = 0));

//assume that at least one target is on first row
asm alw (targetOnFirstRow(cellA[0], cellA[1]) | targetOnFirstRow(cellB[0], cellB[1]) | targetOnFirstRow(cellC[0], cellC[1]));


/*
 * On each patrolling round, the robot visits the locations in the same order A->B->C
 */
sys boolean flagA;
sys boolean flagB;
 
gar ini !flagA;
gar ini !flagB;

sys boolean red;
sys boolean green;

/*The robot starts at location (0,0), the point (0,0) is marked in red. */ 
gar ini red;

gar alw red -> !green;
gar alw !red -> green;

//mark we visit location
/*When the robot visits the first location - A, home is turned green. */
gar alw atCellA -> (flagA & green);
gar alw atCellB -> flagB;
/*When the robot finishes a patrolling round - C, home is turned red. */
gar alw atCellC -> (!flagA & !flagB & red);

//gar order A->B->C 
//if we visited A but not B so we can't visit C (before B)
gar alw flagA & !flagB -> !atCellC;

//if we visit B we, visited A before.
gar alw flagB -> (flagA & !atCellA);

//flags don't False by themselves
gar alw (flagA & next(!atCellC)) -> next(flagA); 
gar alw (flagB & next(!atCellC)) -> next(flagB); 

//flags don't marked by themselves
gar alw (!flagA & next(!atCellA)) -> next(!flagA); 
gar alw (!flagB & next(!atCellB)) -> next(!flagB); 

//red don't turns to green by himself
gar alw (red & next(!atCellA)) -> next(red); 

//green don't turns to red by himself
gar alw (green & next(!atCellC)) -> next(green); 



/*
 * restrict the moves of the robot to go only to neighboring
 * squares
 */
gar robotMoves:
  alw moveDistX(robotX) & moveDistY(robotY); 

/**
 * make sure parameter changes by at most +/- one 
 * @param Dim pos
 */    
predicate moveDistX(DimX pos):
  pos+1 = next(pos) |
  pos   = next(pos) |
  pos-1 = next(pos);  

predicate moveDistY(DimY pos):
  pos+1 = next(pos) |
  pos   = next(pos) |
  pos-1 = next(pos);  
  
