INFO {
  TITLE:       "Simple Arbiter (binary encoded version)"
  DESCRIPTION: "This variant of the simple arbiter avoids the linear rise
in the number of inputs with increasing number of clients. To this
end, a client request is encoded in binary using only logarithmically
many bits. This way only one request can be made per time step, which
is answerd by a respective binary encoded grant the same way. As a
consequence, without further restrictions, every request could be
answered immediately. Hence, to complicate the situation again, the
output player is forced to repeate the same grant for one time step
once at a while. This way, it can be possible for the input player to
open multiple requests faster than they can be answered."
  SEMANTICS:   Mealy
  TARGET:      Mealy
}

GLOBAL {
  PARAMETERS {
n = 64;
  }

  DEFINITIONS {
    /* Checks whether a bus of size log(n) currently represents
     * the numerical value v (encoded in binary).
     */
    value(bus,v) = value'(bus,v,0,SIZEOF bus);
    value'(bus,v,i,j) =
      i >= j        : true
      bit(v,i) == 1 : value'(bus,v,i + 1,j) &&  bus[i]
      otherwise     : value'(bus,v,i + 1,j) && !bus[i];

    /* Returns the i-th bit of the numerical value v. */
    bit(v,i) =
      i <= 0     : v % 2
      otherwise  : bit(v / 2,i - 1);

    /* floor(log2) */
    log2_dn(x) =
      x <= 1    : 0
      otherwise : 1 + log2_dn(x/2);

    /* nbits(x): return the number of bits needed to encode x values
     * This is similar to ceil(log2) except for value 1: nbits(1) = 1 while ceil(log2(1)) = 0
    */
    nbits(x) =
      x == 0    : 0
      otherwise : 1 + log2_dn(x-1);
  }
}

MAIN {

  INPUTS {
    request[nbits(n)];
  }

  OUTPUTS {
    // allow one more value in the outputs indicating no selected
    // grant
    grant[nbits(n+1)];
  }

  INVARIANTS {
    // every request is eventually granted
    &&[0 <= i < n]
      (value(request, i) -> F value(grant, i));
  }

  GUARANTEE {
    // sometimes there has to be a delay to allow requests to be
    // produced faster that they can be granted.
    G F (&&[0 <= i < nbits(n)] (grant[i] <-> X grant[i]));
  }
}
//#!SYNTCOMP
//STATUS : unknown
//REF_SIZE : 0
//#
